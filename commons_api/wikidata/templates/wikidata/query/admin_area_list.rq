SELECT DISTINCT ?adminArea
                (MIN(?primarySort) AS ?primarySort)
                (GROUP_CONCAT(DISTINCT REPLACE(STR(?adminAreaType), '^.*/', ''); SEPARATOR=" ") AS ?adminAreaTypes) {
  {
    VALUES (?adminArea ?primarySort ?adminAreaType) { (wd:{{ country.id }} 1 wd:Q6256) }
  } UNION {
    # Find regional admin areas of this country (generally FLACSen)
    ?adminArea wdt:P17 wd:{{ country.id }} ;
          wdt:P31/wdt:P279* wd:Q10864048
    VALUES (?primarySort ?adminAreaType) { (2 wd:Q10864048) }
  } UNION {
    # Find cities or municipalities with populations of over 250k
    VALUES ?adminAreaType { wd:Q515 wd:Q15284 }
    ?adminArea wdt:P17 wd:{{ country.id }} ;
       wdt:P31/wdt:P279* ?adminAreaType ;
       wdt:P1082 ?population .
    FILTER (?population > 250000)
    VALUES ?primarySort { 3 }
  }

  # Remove admin areas that have ended
  FILTER NOT EXISTS { ?adminArea wdt:P582|wdt:P576 ?adminAreaEnd . FILTER (?adminAreaEnd < NOW()) }
} GROUP BY ?adminArea ORDER BY ?primarySort ?adminArea